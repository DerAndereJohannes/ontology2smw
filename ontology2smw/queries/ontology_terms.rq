PREFIX dc: <http://purl.org/dc/elements/1.1/title>
PREFIX dcterm: <http://purl.org/dc/terms/>
PREFIX ns: <http://www.w3.org/2003/06/sw-vocab-status/ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gndo: <https://d-nb.info/standards/elementset/gnd#>

SELECT DISTINCT ?term
(GROUP_CONCAT(DISTINCT ?termType; separator='@@@@') as ?termType_concat)
(GROUP_CONCAT(DISTINCT ?range; separator='@@@@') as ?range_concat)
(GROUP_CONCAT(DISTINCT ?label; separator='@@@@') as ?label_concat)
(GROUP_CONCAT(DISTINCT ?subclassof; separator='@@@@') as ?subclassof_concat)
(GROUP_CONCAT(DISTINCT ?subpropertyof; separator='@@@@') as ?subpropertyof_concat)

WHERE {
    {
        ?term rdf:type owl:Class.
        BIND (owl:Class as ?termType).
        OPTIONAL {?term rdfs:subClassOf ?subclassof.}
    }
    UNION
    {
        ?term rdf:type owl:DatatypeProperty.
        BIND (owl:DatatypeProperty as ?termType).
        OPTIONAL {?term rdfs:subPropertyOf ?subpropertyof.}
        OPTIONAL{ ?term rdfs:range ?range. }
    }
    UNION
    {
        ?term rdf:type owl:ObjectProperty.
        BIND (owl:ObjectProperty as ?termType).
        OPTIONAL {?term rdfs:subPropertyOf ?subpropertyof.}
    }
    # UNION ## Leaving out owl:AnnotationProperty
    # {
    #     ?term rdf:type owl:AnnotationProperty.
    #     BIND (owl:AnnotationProperty as ?termType).
    #     OPTIONAL {?term rdfs:subPropertyOf ?subclassof.}
    # }
    OPTIONAL{
        ?term rdfs:label ?label.
        FILTER(LCASE(LANG(?label)) = 'en')
    }
    FILTER (!isBlank(?term))
}
GROUP BY ?term
ORDER BY ?term
